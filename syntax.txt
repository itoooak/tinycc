program       = func_def+
func_def      = ident "(" (ident ("," ident)*)? ")" compound_stmt
                /* ただし関数定義の引数は6個まで */
stmt          = expr ";" 
              | "return" expr ";"
              | "if" "(" expr ")" stmt ("else" stmt)?
              | "while" "(" expr ")" stmt
              | "for" "(" expr? ";" expr? ";" expr? ")" stmt
              | compound_stmt
compound_stmt = "{" stmt* "}"
expr          = assign
assign        = equality ("=" assign)?
equality      = relational ("==" relational | "!=" relational)*
relational    = add ("<" add | "<=" add | ">" add | ">=" add)*
add           = mul ("+" mul | "-" mul)*
mul           = unary ("*" unary | "/" unary)*
unary         = ("+" | "-")? primary
              | "&" unary
              | "*" unary
primary       = num
              | ident ("(" (expr ("," expr)*)? ")")?
                /* ただし関数呼び出しの引数は6個まで */
              | "(" expr ")"
